import com.deezer.caupain.model.StabilityLevelPolicy
import com.deezer.caupain.plugin.DependenciesUpdateTask

plugins {
  alias(libs.plugins.android) apply false
  alias(libs.plugins.android.cacheFix) apply false
  alias(libs.plugins.kotlin.android) apply false
  alias(libs.plugins.compose.compiler) apply false
  alias(libs.plugins.spotless) apply false
  alias(libs.plugins.ksp) apply false
  alias(libs.plugins.doctor) apply false
  alias(libs.plugins.caupain) apply false
}

allprojects {
  apply(plugin: libs.plugins.spotless.get().pluginId)

  repositories {
    mavenLocal()
    gradlePluginPortal()
    google()
    mavenCentral()

    maven {
      setUrl("https://jitpack.io")
      content {
        includeGroup("com.github.pyamsoft.cachify")
        includeGroup("com.github.pyamsoft.pydroid")
        includeGroup("com.github.pyamsoft")
        includeGroup("com.github.pyamsoft.ktor")
      }
    }
  }

  configurations.all {
      resolutionStrategy {
          eachDependency { details ->
              if (details.requested.group == 'io.ktor') {
                  details.useTarget group: 'com.github.pyamsoft.ktor', name: details.requested.name, version: '3.3.3-PYAMSOFT'
                  details.because "Force use of Pyamsoft fork from JitPack"
              }
          }
      }
  }

  spotless {
    java {
      target("src/**/*.java")

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
    kotlin {
      target("src/**/*.kt")
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
    kotlinGradle {
      target("*.gradle.kts")
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
    groovyGradle {
      target("*.gradle")
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      leadingTabsToSpaces(2)
    }
  }

  tasks.withType(DependenciesUpdateTask).configureEach {
    selectIf(StabilityLevelPolicy.INSTANCE)
  }
}

subprojects {
  tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-Xlint:unchecked")
    options.compilerArgs.add("-Xlint:deprecation")
    options.deprecation = true
  }
}